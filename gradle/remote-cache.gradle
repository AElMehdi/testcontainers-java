buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.ch.myniva.gradle:s3-build-cache:0.10.0"
        classpath "com.gradle.enterprise:com.gradle.enterprise.gradle.plugin:3.2.1"
    }
}

apply plugin: 'ch.myniva.s3-build-cache'

ext.isMasterBuild = false ||
    (System.getenv("GITHUB_REF") == "refs/heads/master") ||
    (System.getenv("BUILD_SOURCEBRANCHNAME") == "master")

buildCache {
    local {
        enabled = !isMasterBuild
    }

    remote(ch.myniva.gradle.caching.s3.AwsS3BuildCache) {
        endpoint = 'fra1.digitaloceanspaces.com'
        region = 'fra1'
        bucket = 'testcontainers'
        path = 'cache'
        push = isMasterBuild && !System.getenv("READ_ONLY_REMOTE_GRADLE_CACHE")
        headers = [
            'x-amz-acl': 'public-read'
        ]
    }
}
